repositories {
    mavenLocal()
    mavenCentral()
}

def target = System.getProperty("substrate.target")
def step = System.getProperty("substrate.step")

configurations {
    substrate
}

dependencies {
    substrate "com.gluonhq:substrate:$substrateVersion"
}

mainClassName = "com.gluonhq.substrate.test.Main"

task runScript(type: Exec) {
    def javaHome = validateJavaHome()
    def graalvmHome = validateGraalvmHome()

    def skipsigning = System.getProperty("skipsigning")
    def expected = System.getProperty("expected")

    def argsList = ["${javaHome}/bin/java", '-cp', "${project.configurations.substrate.asPath}",
                   "-Dimagecp=${project.sourceSets.main.runtimeClasspath.asPath}",
                   "-Dgraalvm=${graalvmHome}"]
    if (target != null) {
        argsList += ["-DtargetProfile=$target"]
    }
    if (step != null) {
        argsList += ["-Dstep=$step"]
    }
    if (skipsigning != null) {
        argsList += ["-Dskipsigning=$skipsigning"]
    }
    if (expected != null) {
        argsList += ["-Dexpected=$expected"]
    }
    argsList += ["-Dmainclass=$mainClassName", 'com.gluonhq.substrate.SubstrateDispatcher']
    commandLine argsList
}
